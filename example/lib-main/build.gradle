apply plugin: 'com.android.library'
apply plugin: 'com.kezong.fat-aar'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

repositories {
    flatDir {
        dirs 'libs'
    }
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 1
        versionName "1.0.0"
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions "default"

    productFlavors {
        flavor1 {
            missingDimensionStrategy 'default2', 'flavor3'
        }

        flavor2 {
            missingDimensionStrategy 'default2', 'flavor3'
        }
    }

    packagingOptions {
        exclude 'lib/x86/*.so'
        exclude 'lib/x86_64/*.so'
        exclude 'lib/armeabi/*.so'
        exclude 'lib/arm64-v8a/*.so'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_11
    }
}

afterEvaluate {
    // for app test, copy the final aar to application libs
    android.libraryVariants.all { variant ->
        File outputFile = variant.outputs.first().outputFile
        tasks.named("assemble${variant.name.capitalize()}").configure {
            doLast {
                copy {
                    from outputFile
                    into "../app/libs"
                    rename outputFile.name, "fat-aar-final.aar"
                }
            }
        }
    }
}

fataar {
    /**
     * If transitive is true, local jar module and remote library's dependencies will be embed.
     * If transitive is false, just embed first level dependency
     * Local aar project does not support transitive, always embed first level
     * Default value is false
     * @since 1.3.0
     */
    transitive = true

    /**
     * Add prefix for all library resources. To prevent conflicts with a library clients.
     * @since 1.3.7
     */
    resourcePrefix = "lib_main_"
}

dependencies {
    implementation fileTree(dir: 'libs', include: '*.jar')
    // java dependency
    embed project(path: ':lib-java', configuration: 'default')
    // aar dependency
    embed project(path: ':lib-aar', configuration: 'default')
    // aar dependency
    embed project(path: ':lib-aar2', configuration: 'default')
    // local full aar dependency, just build in flavor1
    flavor1Embed project(path: ':lib-aar-local', configuration: 'default')
    // local full aar dependency, just build in debug
    debugEmbed (name:'lib-aar-local2', ext:'aar')
    // remote dependency
    embed 'com.google.guava:guava:20.0'
    // remote aar dependency
    embed ('com.facebook.fresco:fresco:1.12.0') {
        // exclude any group or module
        // exclude(group:'com.facebook.soloader', module:'soloader')
    }
    // remote aar dependency
    embed ('com.github.bumptech.glide:glide:4.11.0') {
        // If transitive = false, it will not embed dependencies declared in pom.xml
        // If fataar.transitive = false, There is no need to declare.
        transitive = false
    }
    // don't want to embed in
    implementation('androidx.appcompat:appcompat:1.2.0')
}

